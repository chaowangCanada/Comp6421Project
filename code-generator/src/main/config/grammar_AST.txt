prog -> classDecl_list ACT_classList funcDef_list ACT_funcDefList MAIN funcBody ACT_prog SEMICOLON
classDecl_list -> classDecl ACT_classDecl classDecl_list | EPSILON
funcDef_list -> funcDef ACT_funcDef funcDef_list | EPSILON
classDecl -> CLASS IDENTIFIER colonIdCommaIdList_op LCP varDeclFuncDecl_list RCP ACT_membList SEMICOLON
colonIdCommaIdList_op -> COLON IDENTIFIER commaId_list ACT_inherList | EPSILON
commaId_list -> COMMA IDENTIFIER commaId_list | EPSILON
varDeclFuncDecl_list -> type IDENTIFIER varDeclFuncDeclTail | EPSILON
varDeclFuncDeclTail -> arraySize_list ACT_dimList SEMICOLON ACT_varDecl varDeclFuncDecl_list | LP fParams ACT_fParamList ACT_fCall RP SEMICOLON ACT_funcDecl varDeclFuncDecl_list
funcHead -> type IDENTIFIER ACT_scopeSpec srId_op LP fParams ACT_fParamList RP
srId_op -> SR IDENTIFIER | EPSILON
funcDef -> funcHead funcBody SEMICOLON
funcBody -> LCP funcBody_ ACT_statBlock RCP
funcBody_ -> typeNumeric IDENTIFIER arraySize_list ACT_dimList ACT_varDecl SEMICOLON funcBody_ | IDENTIFIER ACT_indexList ACT_dataMember ACT_var varDeclStatement_tail | restOfStatment statement_list | EPSILON
varDeclStatement_tail -> IDENTIFIER arraySize_list ACT_dimList ACT_varDecl SEMICOLON funcBody_ | indiceIdnestList_tail assignOp expr ACT_assignStat SEMICOLON statement_list
indiceIdnestList_tail -> DOT IDENTIFIER ACT_dataMember indiceIdnestList_tail | LSP arithExpr RSP indiceIdnestList_tail | LP aParams RP DOT IDENTIFIER ACT_dataMember indiceIdnestList_tail | EPSILON
statement_list -> statement statement_list | EPSILON
arraySize_list -> arraySize arraySize_list | EPSILON
statement -> assignStat ACT_assignStat SEMICOLON | restOfStatment
restOfStatment -> IF LP expr RP THEN statBlock ELSE statBlock SEMICOLON ACT_ifStat | FOR LP type IDENTIFIER assignOp expr ACT_assignStat SEMICOLON relExpr SEMICOLON assignStat ACT_assignStat RP ACT_statBlockStart statBlock SEMICOLON ACT_forStat | READ LP variable RP SEMICOLON ACT_getStat |  WRITE LP expr RP SEMICOLON ACT_putStat | RETURN LP expr RP SEMICOLON ACT_returnStat
assignStat -> variable assignOp expr
statBlock -> LCP statement_list RCP ACT_statBlock | statement | EPSILON
expr -> arithExpr expr_prime
expr_prime -> relOp ACT_relOp arithExpr ACT_relExpr| EPSILON
relExpr -> arithExpr relOp ACT_relOp arithExpr ACT_relExpr
arithExpr -> term arithExpr_prime
arithExpr_prime -> addOp ACT_assignOp term ACT_addOp arithExpr_prime | EPSILON
sign -> ADDITION | SUBTRACTION
term -> factor term_tail
term_tail -> multOp ACT_assignOp factor ACT_multOp term_tail | EPSILON
factor -> LP arithExpr RP | FLOAT_NUM | INT_NUM | NOT factor ACT_not | variable | sign ACT_assignOp factor ACT_sign
variable -> IDENTIFIER variable_
variable_ -> indice_list ACT_indexList ACT_dataMember idnest_list_ ACT_var | LP aParams RP ACT_aParams ACT_fCall idnest_list_ ACT_var
idnest_list_ -> idnest_ idnest_list_ | EPSILON
idnest_ -> DOT IDENTIFIER idnest_prime_
idnest_prime_ -> LSP arithExpr RSP indice_list ACT_indexList ACT_dataMember | LP aParams RP ACT_aParams ACT_fCall | ACT_dataMember EPSILON
indice_list -> indice indice_list | EPSILON
indice -> LSP arithExpr RSP
arraySize -> LSP INT_NUM RSP
type -> IDENTIFIER | typeNumeric
typeNumeric -> FLOAT | INTEGER
fParams -> type IDENTIFIER arraySize_list ACT_dimList ACT_fParam fParamsTail_list | EPSILON
fParamsTail_list -> fParamsTail ACT_dimList ACT_fParam fParamsTail_list | EPSILON
aParams -> expr aParamsTail_list | EPSILON
aParamsTail_list -> aParamsTail aParamsTail_list | EPSILON
fParamsTail -> COMMA type IDENTIFIER arraySize_list
aParamsTail -> COMMA expr
assignOp -> EQ
relOp -> EQEQ | NEQ | LT | GT | LEQ | GEQ
addOp -> ADDITION | SUBTRACTION | OR
multOp -> MULTIPLICATION | DIVISION | AND